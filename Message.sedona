@palette=false
abstract internal class Message extends MessageCommon
{
  ////////////////////////////////////////////////////////////////
  // Properties
  ////////////////////////////////////////////////////////////////
  define Str QoSLevels = "FireAndForget, GuaranteeReceived, GuaranteeReceivedOnce"

  @range=QoSLevels
  @config property byte QoS = 0

  // 0: publish when changed; 1000: publish per 1 sec; 2000: per 2 secs; 3000: ...
  @unit=Units.millisecond
  @config property int publishInterval = 5000

  @unit=Units.millisecond
  @config property int updateInterval = 3000
  
  ////////////////////////////////////////////////////////////////
  // Methods
  ////////////////////////////////////////////////////////////////
  // natives
  ** 
  ** publish message to MQTT broker 
  **  - handle: MQTT session handle 
  **  - topic: message topic 
  **  - payload: message payload 
  **  - payload_len: length of payload 
  **  - qos: MQTT Quality of Service, valid values: 0, 1, 2
  static native void doPublish(Obj handle, Str topic, Str payload, int payload_len, int qos)
  static native void doSubscribe(Obj handle, Str topic, int qos)
  static native void doUnsubscribe(Obj handle, Str topic)
  static native bool fetchData(Obj handle, Str topic, Str buf, int length);

  protected virtual Str getPayload() { return null }

  virtual override void execute()
  {
    if (error != Ok)
      return

    if (publishInterval > 0)
      readyToPublish = (Sys.ticks()-lastPublishTime) >= (long)publishInterval*1ms
  }
  
  ** 
  ** publish linked value to MQTT broker
  ** 
  internal override virtual void publish(Obj handle)
  {
    if (error != Ok)
      return

    if (handle == null)
    {
      log.message("PUBLISH: null handle")
      return
    }

    if (!readyToPublish)
      return

    Str msg = getPayload()
    if (msg == null)
      return

    if (topicChanged) 
    {
      fullTopicName.set(0, 0)
      topicChanged = false
    }

    Str fullTopic = getFullTopic()
    if (fullTopic == null)
      return

    doPublish(handle, fullTopic, msg, msg.length(), QoS)
    lastPublishTime = Sys.ticks()
    readyToPublish = false
  }
  
  ** 
  ** update local slot data from subscribed data
  ** 
  protected virtual bool updateData(Obj handle)
  {
    if (fullTopicName.length() == 0)
      return false

    bool result = fetchData(handle, fullTopicName, strBuf, strLen-1)
    return result
  }
  
  ** 
  ** subscribe topic on MQTT broker refered by given handle
  ** 
  internal override virtual void subscribe(Obj handle)
  {
    if (error != Ok)
      return

    if (handle == null)
    {
      log.message("SUBSCRIBE: null handle")
      return
    }

    if (topicChanged)
      unsubscribe(handle)

    if (subscribedToTopic)
    {
      long now = Sys.ticks()
      if (now - lastUpdateTime > (long)updateInterval*1ms) {
        updateData(handle)
        lastUpdateTime = now
      }
    }
    else
    {
      Str fullTopic = getFullTopic()
      if (fullTopic == null)
        return

      log.message("SUBSCRIBE: topic=${fullTopic}")
      doSubscribe(handle, fullTopic, QoS)
      subscribedToTopic = true
    }
  }
  
  ** 
  ** unsubscribe topic on MQTT broker refered by given handle
  ** 
  internal override virtual void unsubscribe(Obj handle)
  {
    if (error != Ok)
      return

    if (handle == null)
    {
      log.message("UNSUBSCRIBE: null handle")
      return
    }

    topicChanged = false
    if (subscribedToTopic)
    {
      log.message("UNSUBSCRIBE: topic=${fullTopicName}")
      doUnsubscribe(handle, fullTopicName)
      subscribedToTopic = false
    }
    fullTopicName.set(0, 0)
  }

  **
  ** mark unscription needed, the topic will be unsubscribed when worker instance call us 
  ** 
  internal override virtual void onTopicChanged()
  {
    super.onTopicChanged()
    topicChanged = true
  }

  internal override virtual void changed(Slot slot) 
  {
    super.changed(slot)
    if (error == Ok && slot.name == "payload" && publishInterval == 0)
      readyToPublish = true
  }
  
  ** 
  ** reset current message's local state 
  ** 
  internal override virtual void reset()
  {
    readyToPublish = false
    subscribedToTopic = false
    topicChanged = false
    lastPublishTime = 0L
    lastUpdateTime = 0L
  }

  protected Str getFullTopic() 
  {
    if (fullTopicName.length() == 0)
      generateFullTopic()
    return fullTopicName
  }

  protected void generateFullTopic()
  {
    //unsubscribe old topic if needed
    if (fullTopicName.length() > 0 && subscribedToTopic)
      unsubscribe(getHandle())

    fullTopicName.set(0, 0)
    collectTopics(fullTopicName, fullTopicNameLen)
  }
  ////////////////////////////////////////////////////////////////
  // Fields
  ////////////////////////////////////////////////////////////////
  define int strLen = 64
  define int fullTopicNameLen = 400

  private bool readyToPublish = false
  private bool subscribedToTopic = false
  private bool topicChanged = false
  private long lastPublishTime = 0L
  private long lastUpdateTime = 0L
  
  protected inline Str(strLen) strBuf = ""
  protected inline Str(fullTopicNameLen) fullTopicName = ""

  internal define Log log
}
